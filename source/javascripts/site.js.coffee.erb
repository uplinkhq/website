#= require jquery-smooth-scroll
#= require js-cookie
#= require bootstrap/button
#= require bootstrap/collapse

origin = window.location.protocol + '//' + window.location.host
if origin != "<%= host %>"
  correctUrl = window.location.href.replace(origin, "<%= host %>")
  window.location.replace correctUrl

$ ->
  $('.navbar a[href*="#"]').smoothScroll()
  showCookieBar()
  submitFormsViaAjax()

showCookieBar = ->
  unless Cookies.get('cookie-consent')?
    cookieBar = $('#cookie-bar')
    cookieBar
      .slideDown()
      .find('a')
      .click (e) ->
        e.preventDefault()
        Cookies.set 'cookie-consent', 'true',
          expires: 365
          path:    '/'
        cookieBar.slideUp()

submitFormsViaAjax = ->
  $('form').submit (e) ->
    e.preventDefault()
    $submitButton = $(@).find('input[type="submit"]').button('loading')
    $.ajax @action,
      type:     'POST'
      data:     $(@).serialize()
      dataType: 'json'
    .always =>
      $submitButton.button('reset')
    .done =>
      $(@)
        .slideUp()
        .siblings('.alert-success')
        .slideDown()
    .fail (xhr, status) =>
      Rollbar.error 'Error submitting form.', xhr: xhr, status: status
      $(@)
        .siblings('.alert-danger')
        .slideDown()
