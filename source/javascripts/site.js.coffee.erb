#= require turbolinks
#= require jquery.lazyload
#= require jquery-smooth-scroll/jquery.smooth-scroll
#= require js-cookie
#= require autosize
#= require bootstrap/collapse
#= require bootstrap/tooltip
#= require bootstrap/dropdown
#= require bootstrap/modal
#= require @fortawesome/fontawesome-pro/js/all

$(document).on 'shown.bs.modal', '#freelancer-request-modal', ->
  id = 'typeform-script'
  unless document.getElementById(id)
    js = document.createElement('script')
    js.id = id
    js.src = 'https://s3-eu-west-1.amazonaws.com/share.typeform.com/widget.js'
    firstScript = document.getElementsByTagName('script')[0]
    firstScript.parentNode.insertBefore(js, firstScript)

$(document).on 'submit', 'form', (e) ->
  e.preventDefault()

  $submit = $(@).find(':submit')
  $submit.data('original-text', $submit.text())
  $submit.text($submit.data('loading'))
  $submit.addClass('disabled')

  $.ajax @action,
    type:     'POST'
    data:     $(@).serialize()
    dataType: 'json'
  .always =>
    $submit.removeClass('disabled')
    $submit.text($submit.data('original-text'))
    $(@)
      .siblings('.note')
      .slideUp()
  .done =>
    $(@)
      .slideUp()
      .siblings('.note.note-confirm')
      .slideDown()
  .fail (xhr, status) =>
    if Rollbar?
      Rollbar.error 'Error submitting form.', xhr: xhr, status: status
    $(@)
      .siblings('.note.note-error')
      .slideDown()

showCookieBar = ->
  unless Cookies.get('cookie-consent')?
    cookieBar = $('#cookie-bar')
    cookieBar
      .slideDown()
      .find('a')
      .click (e) ->
        e.preventDefault()
        Cookies.set 'cookie-consent', 'true',
          expires: 365
          path:    '/'
        cookieBar.slideUp()

# Hack to get the "position: fixed" element
# with the bg image to have the same height as the header.
showHeaderBg = ->
  $headerBg = $('header .bg')
  if $headerBg.css('background-image') != 'none'
    $headerBg.css('height', "#{$headerBg.parent().height()}px")
             .show()

$(document).on 'turbolinks:load', ->
  showHeaderBg()
  $('a[href^="#"]').smoothScroll()
  $('img.lazy').show().lazyload(effect: 'fadeIn')
  $('[data-toggle="tooltip"]').tooltip()
  autosize(document.querySelectorAll('.autosize'))
  showCookieBar()
  FontAwesome.dom.i2svg()

  if $('body#companies').length
    if window.location.hash == '#freelancer-request'
      $('#freelancer-request-modal').modal('show')
