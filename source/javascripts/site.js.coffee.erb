#= require turbolinks
#= require jquery.lazyload
#= require jquery-smooth-scroll/jquery.smooth-scroll
#= require js-cookie
#= require autosize
#= require bootstrap/collapse
#= require bootstrap/tooltip
#= require bootstrap/dropdown
#= require @fortawesome/fontawesome-pro/js/all

#= require_tree ./pages

$(document)

  .on 'submit', 'form', (e) ->
    e.preventDefault()

    $submit = $(@).find(':submit')
    $submit.data('original-text', $submit.text())
    $submit.text($submit.data('loading'))
    $submit.prop('disabled', true)

    $.ajax @action,
      type:     'POST'
      data:     $(@).serialize()
      dataType: 'json'
    .always =>
      $submit.prop('disabled', false)
      $submit.text($submit.data('original-text'))
      $(@)
        .siblings('.note')
        .slideUp()
    .done =>
      $(@)
        .trigger('submit-success')
        .slideUp()
        .siblings('.note.note-confirm')
        .slideDown()
    .fail (xhr, status) =>
      if Rollbar?
        Rollbar.error 'Error submitting form.', xhr: xhr, status: status
      $(@)
        .trigger('submit-error')
        .siblings('.note.note-error')
        .slideDown()

showCookieBar = ->
  unless Cookies.get('cookie-consent')?
    cookieBar = $('#cookie-bar')
    cookieBar
      .slideDown()
      .find('a')
      .click (e) ->
        e.preventDefault()
        Cookies.set 'cookie-consent', 'true',
          expires: 365
          path:    '/'
        cookieBar.slideUp()

# Hack to get the "position: fixed" element
# with the bg image to have the same height as the header.
showHeaderBg = ->
  $headerBg = $('header .bg')
  if $headerBg.css('background-image') != 'none'
    $headerBg.css('height', "#{$headerBg.parent().height()}px")
             .show()

$(document).on 'turbolinks:load', ->
  showHeaderBg()
  $('a[href^="#"]').smoothScroll()
  $('img.lazy').show().lazyload(effect: 'fadeIn')
  $('[data-toggle="tooltip"]').tooltip()
  autosize(document.querySelectorAll('.autosize'))
  showCookieBar()
  FontAwesome.dom.i2svg()
