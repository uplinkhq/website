bodySelector = 'body#companies'

$(document)

  .on 'click', "#{bodySelector} #freelancer-request .option", ->
    window.location.hash = $(@).data('url-fragments').split(' ')[0]

    unless $(@).hasClass('active')
      $container = $(@).parents('.container')

      toggleables = ['.option', '.option-fields']
      # If one of the confirmations is already visible, don't toggle it anymore.
      unless $container.find('.note-confirm').is(':visible')
        toggleables.push '.option-confirmation'
      $container.find(toggleables.join(', ')).toggleClass('active')

      $container.find('.option-fields :input').prop('disabled', (_, val) -> !val)

      initRateSlider()

  .on 'click', "#{bodySelector} a[href^='#']", (e) ->
    e.preventDefault()
    enableOption $(@).attr('href').replace(/^#/, '')

  .on 'turbolinks:load', ->
    if $(bodySelector).length && window.location.hash.length
      enableOption window.location.hash.replace(/^#/, '')

  .on 'submit-success', "#{bodySelector} #freelancer-request form", ->
    # Track via LinkedIn
    image = document.createElement('img')
    image.src = 'https://dc.ads.linkedin.com/collect/?pid=1099289&conversionId=939857&fmt=gif'
    document.body.appendChild(image)

    # Track via Google Analytics
    if gtag?
      gtag 'event', 'generate_lead', event_label: $('body').data('locale')

enableOption = (option) ->
  $el = $(".option[data-url-fragments~='#{option}']")
  if $el.length
    $.smoothScroll scrollTarget: '#freelancer-request'
    $el.click()

initRateSlider = ->
  if sliderEl = $('#rate-slider:visible:empty').get(0)
    noUiSliderVersion = '12.1.0'

    stylesheetId = 'nouislider-css'
    unless document.getElementById(stylesheetId)
      linkEl = document.createElement('link')
      linkEl.setAttribute('rel', 'stylesheet')
      linkEl.setAttribute('id', stylesheetId)
      linkEl.setAttribute('href', "https://cdnjs.cloudflare.com/ajax/libs/noUiSlider/#{noUiSliderVersion}/nouislider.min.css")
      document.getElementsByTagName('head')[0].appendChild(linkEl)

    scriptUrl = "https://cdnjs.cloudflare.com/ajax/libs/noUiSlider/#{noUiSliderVersion}/nouislider.min.js"
    $.ajax url: scriptUrl, dataType: 'script', cache: true, success: ->
      [min, max] = [60, 150]
      noUiSlider.create sliderEl,
        start:    [min, max]
        step:     5
        tooltips: true
        connect:  [false, true, false]
        format:
          to:   (value) -> Math.round(value) + ' €' # Need to use `Math.round` here since otherwise 125 is displayed as 124.9999...
          from: (value) -> value.replace(' €', '')
        range:
          min: min
          max: max

      sliderEl.noUiSlider.on 'update', (values) ->
        $('input[name="rate"]').val $.map(values, (value) -> parseInt(value))
